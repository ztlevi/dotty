#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source $DOTTY_CONFIG_HOME/shell/zsh/.zshenv

install() {
  # Base utilities
  brew install zsh bash wget git curl tree rsync p7zip jq direnv

  # Use trash instead of rm
  case $(_os) in
    linux-*) brew install trash-cli;;
    macos) brew install trash;;
  esac

  # Better console tools
  brew install eza ripgrep fd bat hyperfine procs sd duf
  if [[ $USER != "runner" ]]; then # disable this section for
    ! topic-enabled-p dev/rust && dotty dev/rust
    cargo install dirstat-rs
  fi

  # Other utilities
  brew install shfmt shellcheck neofetch mosh

  # True color and Italic support https://github.com/syl20bnr/spacemacs/wiki/Terminal
  if _is_callable /usr/bin/tic; then
      /usr/bin/tic -x -o ~/.terminfo $DOTTY_CONFIG_HOME/shell/zsh/xterm-24bit.terminfo
  else
      echo-fail "/usr/bin/tic does not exists!"
  fi

  # Increase max file watch for editors and unison
  case $(_os) in
    macos)
      sudo sysctl kern.maxfiles=524288 kern.maxfilesperproc=262144
      sudo cat <<EOF | sudo tee -a /etc/sysctl.conf
kern.maxfiles=524288
kern.maxfilesperproc=262144
EOF
      ;;
    linux-*)
      sudo sysctl -w fs.inotify.max_user_watches=524288
      sudo cat <<EOF | sudo tee -a /etc/sysctl.conf
fs.inotify.max_user_watches = 524288
EOF
      ;;
  esac

  # Set default shell to zsh
  chsh -s $(which zsh) $(whoami)
  # if the command above doesn't work, edit /etc/passwd directly to change the login shell

  local zsh="$(which zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
}

update() {
  echo-info "zinit self-update"
  echo-info "zinit update --parallel"
}

link() {
  rm -rf ~/.zinit/snippets/*.config--*--config--shell--zsh
  rm -f ~/.profile
  mklink $DOTTY_CONFIG_HOME/shell/zsh/.zsh* ~/
  mklink $DOTTY_CONFIG_HOME/shell/zsh/.p10k.zsh ~/
  mklink $DOTTY_CONFIG_HOME/shell/zsh/config/* $XDG_CONFIG_HOME/
  mklink $DOTTY_CONFIG_HOME/shell/zsh/completions $XDG_CONFIG_HOME/zsh/

  # Create wgetrc
  [[ ! -f $WGETRC ]] && touch $WGETRC
}

clean() {
  rm -f ~/.zshrc
  rm -f ~/.zshenv
  rm -f $XDG_CONFIG_HOME/starship.toml
}

init "$@"
